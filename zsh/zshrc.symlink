# if we already loaded, perform verbose logging
if [[ $ZSH_LOADED == true ]] ; then
	#export LOG_LEVEL=debug
	export LOG_LEVEL=info
else
	export LOG_LEVEL=info
fi

# logger
debug() {
	if [[ $LOG_LEVEL == "debug" ]]; then
		echo "$fg_bold[grey]$(gdate --iso-8601=ns) $fg_bold[green]$1"
	fi
}

# your project folder that we can `c [tab]` to
export PROJECTS=$HOME/Projects
if [[ $ZSH_LOADED != true ]]; then
	export CDPATH=$PROJECTS
fi
debug "\$CDPATH: '$CDPATH'"

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export SHELL=$(which zsh)

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
debug "$HOME/.localrc"
if [[ -a $HOME/.localrc ]]; then
  source $HOME/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)
sh_common_files=($ZSH/**/*.sh-common)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  debug "$file"
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  debug "$file"
  source $file
done

# load common sh files
for file in ${${sh_common_files}}
do
  debug "$file"
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
debug "compinit"
autoload -U compinit && compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  debug "$file"
  source $file
done

unset config_files

export EDITOR=vim

# shift-tab reverse complete
bindkey '^[[Z' reverse-menu-complete

# allow C-a | C-e in tmux
bindkey -e
bindkey '^R' history-incremental-search-backward

# prepend space allow
setopt histignorespace

# Load this manually, since fzf breaks in tmux if out of order
debug "$HOME/.fzf.zsh"
[ -f $HOME/.fzf.zsh ] && source $HOME/.fzf.zsh

export ZSH_LOADED=true
